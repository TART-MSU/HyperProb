mdp

// 3 | x x x x x
// 2 | x   x   x
// 1 | x   x   x
// 0 | x   x   x
// y ____________
//   x 0 1 2 3 4

// walls in the maze
formula u = y<3 & (x=0 | x=2 | x=4);
formula r = y=3 & x<4;
formula d = y>0 & (x=0 | x=2 | x=4);
formula l = y=3 & x>0;

// updates of coordinates (if possible)
formula yu = u ? (y+1) : y;
formula xr = r ? (x+1) : x;
formula yd = d ? (y-1) : y;
formula xl = l ? (x-1) : x;

// trap state
formula bad = (x=0&y=2) | (x=4&y=2);

// specification of the maze
// for every choice, there is still some small probability of not actually going where desired
module maze
    x : [0..4];
    y : [0..3];
    // moving around the maze (all combinations)
    [up]       !bad  -> 0.8: (y'=yu) + 0.08: (x'=xr) + 0.08: (x'=xl) + 0.04: (y'=yd);
    [right]    !bad  -> 0.8: (x'=xr) + 0.08: (y'=yu) + 0.08: (y'=yd) + 0.04: (x'=xl);
    [down]     !bad  -> 0.8: (y'=yd) + 0.08: (x'=xr) + 0.08: (x'=xl) + 0.04: (y'=yu);
    [left]     !bad  -> 0.8: (x'=xl) + 0.08: (y'=yu) + 0.08: (y'=yd) + 0.04: (x'=xr);
endmodule

// we have two initial states
init (x=0&y=3 | x=2&y=2) endinit

// goal of the robot
label "target" = (x=2)&(y=0);
label "start0" = (x=0)&(y=3);
label "start1" = (x=2)&(y=2);

label "x0y0" = (x=0)&(y=0);
label "x0y1" = (x=0)&(y=1);
label "x0y2" = (x=0)&(y=2);
label "x0y3" = (x=0)&(y=3);

label "x1y0" = (x=1)&(y=0);
label "x1y1" = (x=1)&(y=1);
label "x1y2" = (x=1)&(y=2);
label "x1y3" = (x=1)&(y=3);

label "x2y0" = (x=2)&(y=0);
label "x2y1" = (x=2)&(y=1);
label "x2y2" = (x=2)&(y=2);
label "x2y3" = (x=2)&(y=3);

label "x3y0" = (x=3)&(y=0);
label "x3y1" = (x=3)&(y=1);
label "x3y2" = (x=3)&(y=2);
label "x3y3" = (x=3)&(y=3);

label "x4y0" = (x=4)&(y=0);
label "x4y1" = (x=4)&(y=1);
label "x4y2" = (x=4)&(y=2);
label "x4y3" = (x=4)&(y=3);
